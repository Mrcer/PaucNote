# 实验 9-多周期处理器实验
## 实验内容
设计多周期处理器
## 实验过程
![电路图](./%E7%94%B5%E8%B7%AF%E5%9B%BE.png)
根据电路图设计数据通路，代码如下：
```verilog
`timescale 1ns / 1ps
module Datapath(
	input wire clk, rst,
	input wire [4:0] debug_addr,
	input wire [31:0] mem_rd,
	input wire PC_we,
	input wire [1:0] PC_src,
	input wire IR_we, reg_dst, reg_we, 
	input wire extop, alu_srcA,
	input wire [1:0] alu_srcB, 
	input wire [2:0] alu_ctr,
	input wire IorD, memtoreg,
    output wire [31:0] mem_addr,
    output wire [31:0] mem_wd,
    output wire [31:0] instr,
    output wire [31:0] debug_data,
	output wire overflow, zero
	);
	
	// IF
	wire [31:0] PC_nxt, PC_addr;
	FloprE PC(clk, rst, PC_we, PC_nxt, PC_addr);
	FloprE IR(clk, rst, IR_we, mem_rd, instr);

	// ID
	wire [4:0] reg_wa;
	wire [31:0] reg_wd, reg_rd1, reg_rd2, rd1R_out, rd2R_out;
	Mux2 reg_wamux(reg_dst, instr[20:16], instr[15:11], reg_wa);
	Regfile regfile(~clk, reg_we, instr[25:21], instr[20:16], reg_wa, debug_addr, reg_wd, reg_rd1, reg_rd2, debug_data);
	FloprE rd1R(clk, rst, 1, reg_rd1, rd1R_out);
	FloprE rd2R(clk, rst, 1, reg_rd2, rd2R_out);

	// EXE
	wire [31:0] alu_A, alu_B, alu_out, aluR_out;
	Mux2 alu_muxA(alu_srcA, PC_addr, rd1R_out, alu_A);
	wire [31:0] imm, boffset;
	Signext alu_sxt(extop, instr[15:0], imm);
	Sl2 alu_sl2(imm, boffset);
	Mux4 alu_muxB(alu_srcB, rd2R_out, 4, imm, boffset, alu_B);
	Alu alu(alu_A, alu_B, alu_ctr, alu_out, overflow, zero);
	FloprE aluR(clk, rst, 1, alu_out, aluR_out);

	// PC addr
	wire [31:0] PC_jaddr;
	Mux4 PC_mux(PC_src, alu_out, aluR_out, {{PC_addr[31:28]}, {instr[25:0]}, 2'b00}, 0, PC_nxt);

	// MEM
	Mux2 mem_mux(IorD, PC_addr, aluR_out, mem_addr);
	assign mem_wd = rd2R_out;
	wire [31:0] MDR_out;
	FloprE MDR(clk, rst, 1, mem_rd, MDR_out);

	// WB
	Mux2 reg_wdmux(memtoreg, aluR_out, MDR_out, reg_wd);
endmodule
```
其中的部件可以基于单周期处理器的部件稍作修改得到。
然后设计控制器，ALU控制器可以直接使用单周期处理器的，主控制器要重新设计为状态机，其中状态图和每个状态的输出信号如下：
![状态图](./%E7%8A%B6%E6%80%81%E8%A1%A8.png)
![输出信号](./%E7%8A%B6%E6%80%81%E6%9C%BA.png)
将其转换为代码如下：
```verilog
`timescale 1ns / 1ps
module MainController(
	input wire clk, rst,
	input wire[5:0] op,
	input wire zero,
	output wire PC_we,
    output reg [1:0] PC_src,
	output reg IR_we, reg_dst, reg_we,
	output reg extop, alu_srcA,
	output reg [1:0] alu_srcB,
	output reg IorD, memtoreg, mem_we,
	output reg [1:0] alu_op
    );
	
	reg [3:0] state, nxt;

	always @(negedge clk, posedge rst) begin
		if(rst)
			state <= 4'b0;
		else
			state <= nxt;
	end

	always @(*) begin
		case(state)
			0: nxt <= 1;
			1: begin
				case(op)
					6'b000100: nxt <= 2;	// beq
					6'b000010: nxt <= 3;	// jump
					6'b001000: nxt <= 4;	// addi
					6'b001101: nxt <= 4;	// ori
					6'b000000: nxt <= 6;	// r-type
					6'b100011: nxt <= 8;	// lw
					6'b101011: nxt <= 8;	// sw
					default:   nxt <= 0;	// skip
				endcase
			end
			2: nxt <= 0;
			3: nxt <= 0;
			4: nxt <= 5;
			5: nxt <= 0;
			6: nxt <= 7;
			7: nxt <= 0;
			8: nxt <= op == 6'b100011 ? 10 : 9;
			9: nxt <= 0;
			10: nxt <= 11;
			11: nxt <= 0;
			default: nxt <= 0;
		endcase
	end
	
	reg PC_write, PC_wirtecond;
	assign PC_we = PC_write | (PC_wirtecond & zero);
	always @(*) begin
		if(state == 0 | state == 3)
			PC_write <= 1;
		else
			PC_write <= 0;

		if(state == 0 | state == 1)
			alu_srcA <= 0;
		else
			alu_srcA <= 1;

		if(state == 6 | state == 2)
			alu_srcB <= 0;
		else if(state == 0)
			alu_srcB <= 1;
		else if(state == 1)
			alu_srcB <= 3;
		else
			alu_srcB <= 2;

		if(state == 11)
			memtoreg <= 1;
		else
			memtoreg <= 0;

		if(state == 5 | state == 7 | state == 11)
			reg_we <= 1;
		else
			reg_we <= 0;
		
		if(state == 7)
			reg_dst <= 1;
		else
			reg_dst <= 0;

		if(state == 9)
			mem_we <= 1;
		else
			mem_we <= 0;

		if(state == 0)
			IR_we <= 1;
		else
			IR_we <= 0;

		extop <= 1;

		if(state == 3)
			PC_src <= 2;
		else if(state == 2)
			PC_src <= 1;
		else
			PC_src <= 0;

		if(state == 4)
			alu_op <= 2'b10;
		else if(state == 6)
			alu_op <= 2'b11;
		else if(state == 2)
			alu_op <= 2'b01;
		else
			alu_op <= 2'b00;

		if(state == 2)
			PC_wirtecond <= 1;
		else
			PC_wirtecond <= 0;

		if(state == 9 | state == 10)
			IorD <= 1;
		else
			IorD <= 0;
	end
endmodule
```
最后将数据通路、控制器和内存、显示器等其他组件连接起来即可。
## 实验结果分析
在内存中写入测试代码：
![coe指令说明](./coe%E6%8C%87%E4%BB%A4%E8%AF%B4%E6%98%8E.jpg)
并在结尾加入`08000012`用于阻止PC改变。
仿真测试结果如下
![仿真测试1](./%E4%BB%BF%E7%9C%9F%E6%95%B0%E6%8D%AE1.png)
![仿真测试2](./%E4%BB%BF%E7%9C%9F%E6%95%B0%E6%8D%AE2.png)
分析控制器状态、IR输出和寄存器堆状态，确定仿真结果正确。
烧录程序上板实验，结果如图
![上板实验](./%E5%AE%9E%E9%AA%8C%E7%85%A7%E7%89%87.jpg)
## 实验总结
多周期处理器与单周期处理器最大的不同是控制器改为状态机、数据通路加入中间寄存器划分阶段。